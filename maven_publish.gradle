apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: gcxMavenRepositoryDeploy) {
                authentication(userName: gcxMavenRepositoryUsername, password: gcxMavenRepositoryPassword)
            }
            snapshotRepository(url: gcxMavenRepositorySnapshotDeploy) {
                authentication(userName: gcxMavenRepositoryUsername, password: gcxMavenRepositoryPassword)
            }

            pom.project {
                scm {
                    connection POM_SCM_CONNECTION
                    developerConnection POM_SCM_CONNECTION
                }
            }
        }
    }
}

afterEvaluate {
    if (project.plugins.hasPlugin('java')) {
        task artifactSourcesJar(type: Jar) {
            from sourceSets.main.allSource
            classifier = 'sources'
        }
    }
    if (project.plugins.hasPlugin('com.android.library')) {
        apply plugin: 'com.github.dcendents.android-maven'
    }
    if (project.plugins.hasPlugin('com.android.application') || project.plugins.hasPlugin('com.android.library')) {
        task artifactSourcesJar(type: Jar) {
            classifier = 'sources'
            from android.sourceSets.main.java.srcDirs
        }

        task javadoc(type: Javadoc) {
            source = android.sourceSets.main.java.srcDirs
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            failOnError = false
        }
    }
    if (project.plugins.hasPlugin('java') || project.plugins.hasPlugin('com.android.application') || project.plugins.hasPlugin('com.android.library')) {
        task artifactJavadocJar(type: Jar, dependsOn: 'javadoc') {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }
    }
}
