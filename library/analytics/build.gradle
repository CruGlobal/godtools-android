android {
    defaultConfig {
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: 'org.cru.godtools.analytics.AnalyticsEventBusIndex']
            }
        }

        buildConfigField "String", "GOOGLE_ANALYTICS_CLIENT_ID", "\"UA-325725-50\""
        buildConfigField "String", "SNOWPLOW_ENDPOINT", "\"s.cru.org\""
        consumerProguardFiles 'proguard-rules-adobe.pro'
        consumerProguardFiles 'proguard-rules-snowplow.pro'
    }

    buildTypes {
        debug {
            buildConfigField "String", "SNOWPLOW_APP_ID", "\"godtools-dev\""
        }
        release {
            buildConfigField "String", "SNOWPLOW_APP_ID", "\"godtools\""
        }
    }
}

dependencies {
    implementation project(':library:base')

    implementation "com.google.android.gms:play-services-analytics:${deps.playServices.analytics}"
    implementation "com.google.firebase:firebase-core:${deps.firebase.core}"

    implementation "org.ccci.gto.android:gto-support-compat:${deps.gtoSupport}"
    implementation "org.ccci.gto.android:gto-support-okhttp3:${deps.gtoSupport}"

    implementation "com.adobe.mobile:adobeMobileLibrary:${deps.adobeMobileServices}"
    implementation "com.google.guava:guava:${deps.guava}"
    implementation "com.squareup.okhttp3:okhttp:${deps.okhttp3}"
    implementation "com.snowplowanalytics:snowplow-android-tracker:${deps.snowplow}"
    implementation "me.thekey.android:thekey-api:${deps.thekey}"
    implementation "me.thekey.android:thekey-eventbus:${deps.thekey}"
    api "org.greenrobot:eventbus:${deps.eventbus}"

    debugImplementation "com.jakewharton.timber:timber:${deps.timber}"

    annotationProcessor "org.greenrobot:eventbus-annotation-processor:${deps.eventbus}"
}
