apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'newrelic'
apply plugin: 'org.ajoberstar.grgit'

android {
    compileSdkVersion compileSdk

    defaultConfig {
        applicationId 'org.keynote.godtools.android'
        versionName rootProject.version
        versionCode grgit.log(includes:['HEAD']).size() + 4029047

        minSdkVersion minSdk
        targetSdkVersion targetSdk

        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: 'org.cru.godtools.AppEventBusIndex']
            }
        }

        // Crashlytics config
        ext.betaDistributionGroupAliasesFilePath = "crashlytics/beta_groups.txt"
        ext.betaDistributionNotifications = false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        preDexLibraries !project.hasProperty('disablePreDex')
    }
    lintOptions {
        lintConfig rootProject.file("analysis/lint/lint.xml")
    }
    packagingOptions {
        pickFirst 'META-INF/NOTICE.txt'
        pickFirst 'META-INF/LICENSE.txt'
    }
    signingConfigs {
        release {
            storeFile file(androidKeystorePath)
            storePassword androidKeystoreStorePassword
            keyAlias androidKeystoreKeyAlias
            keyPassword androidKeystoreKeyPassword
        }
    }

    flavorDimensions "env"
    productFlavors {
        stage {
            dimension "env"
        }
        production {
            dimension "env"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            multiDexEnabled true

            minifyEnabled false
            shrinkResources false
        }
        release {
            shrinkResources true
            minifyEnabled true
            if (signingConfigs.release.storeFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    variantFilter { variant ->
        // stage product flavors
        if (variant.flavors*.name.contains("stage")) {
            // ignore for non-debug build types
            if (!variant.buildType.name.equals("debug")) {
                setIgnore(true)
            }
        }
    }
}

dependencies {
    implementation project(':library:api')
    implementation project(':library:base')
    implementation project(':library:base-app')
    implementation project(':library:base-ui')
    implementation project(':library:config')
    implementation project(':library:db')
    implementation project(':library:download-manager')
    implementation project(':library:everystudent')
    implementation project(':library:initial-content')
    implementation project(':library:model')
    implementation project(':library:model-events')
    implementation project(':library:model-loader')
    implementation project(':library:sync')
    implementation project(':library:tract-renderer')

    implementation "com.android.support:appcompat-v7:${deps.androidSupport}"
    implementation "com.android.support:cardview-v7:${deps.androidSupport}"
    implementation "com.android.support:customtabs:${deps.androidSupport}"
    implementation "com.android.support:design:${deps.androidSupport}"
    implementation "com.android.support:percent:${deps.androidSupport}"

    implementation "org.ccci.gto.android:gto-support-appcompat:${deps.gtoSupport}"
    implementation "org.ccci.gto.android:gto-support-eventbus:${deps.gtoSupport}"
    implementation "org.ccci.gto.android:gto-support-picasso:${deps.gtoSupport}"
    implementation "org.ccci.gto.android:gto-support-recyclerview:${deps.gtoSupport}"
    implementation "org.ccci.gto.android:gto-support-sync:${deps.gtoSupport}"

    implementation "com.crashlytics.sdk.android:crashlytics:${deps.crashlytics}"

    implementation "com.evernote:android-job:${deps.evernoteAndroidJob}"
    implementation "com.google.guava:guava:${deps.guava}"
    implementation "com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:${deps.advrecyclerview}"
    implementation "com.jakewharton:butterknife:${deps.butterknife}"
    implementation "uk.co.chrisjenx:calligraphy:${deps.calligraphy}"

    debugImplementation "com.android.support:multidex:${deps.multidex}"
    debugImplementation "org.ccci.gto.android:gto-support-api-okhttp3:${deps.gtoSupport}"
    debugImplementation "org.ccci.gto.android:gto-support-leakcanary:${deps.gtoSupport}"
    debugImplementation "org.ccci.gto.android:gto-support-stetho:${deps.gtoSupport}"
    debugImplementation "com.facebook.stetho:stetho:${deps.facebookStetho}"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:${deps.facebookStetho}"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${deps.leakcanary}"

    annotationProcessor "com.jakewharton:butterknife-compiler:${deps.butterknife}"
    annotationProcessor "org.greenrobot:eventbus-annotation-processor:${deps.eventbusAnnotations}"

    // fix conflicts in transitive dependencies
    androidTestImplementation "com.android.support:multidex:${deps.multidex}"
    androidTestImplementation "com.google.code.findbugs:jsr305:2.0.1"
}
