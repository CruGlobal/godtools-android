buildscript {
    repositories {
        mavenLocal()
        jcenter()
        google()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        //noinspection GradleDependency
        classpath 'com.jakewharton:butterknife-gradle-plugin:8.5.1'
        classpath 'com.newrelic.agent.android:agent-gradle-plugin:5.14.0'
        classpath 'io.fabric.tools:gradle:1.25.2'
    }
}

plugins {
    id "org.ajoberstar.grgit" version "2.1.1" apply false
}

group 'org.cru.android'
version '5.0.11-SNAPSHOT'

// define dependency versions for this project
ext {
    compileSdk = 27
    minSdk     = 15
    targetSdk  = 27
}
ext.deps = [
        advrecyclerview      : '0.10.6',
        androidInstantApps   : '1.1.0',
        androidLifecycle     : '1.1.0',
        androidSupport       : '27.1.1',
        androidTestingSupport: '1.0.0',
        butterknife          : '8.7.0',
        calligraphy          : '2.3.0',
        crashlytics          : '2.9.1',
        eventbus             : '3.0.0',
        eventbusAnnotations  : '3.0.1',
        evernoteAndroidJob   : '1.2.5',
        facebookStetho       : '1.5.0',
        gtoSupport           : '1.2.0-SNAPSHOT',
        guava                : '22.0-android',
        hamcrest             : '1.3',
        junit                : '4.12',
        leakcanary           : '1.5.1',
        lightweightStream    : '1.2.0',
        multidex             : '1.0.3',
        newrelic             : '5.14.0',
        playServices         : '10.2.6',
        retrofit2            : '2.3.0',
        snowplow             : '0.6.2',
        timber               : '4.7.0'
]

subprojects {
    repositories {
        mavenLocal()
        jcenter()
        google()
        maven { url 'https://cruglobal.jfrog.io/cruglobal/list/maven-locals/' }
        maven {
            name "maven.gcx.org"
            url "https://maven.gcx.org/"
            credentials {
                username gcxMavenRepositoryUsername
                password gcxMavenRepositoryPassword
            }
        }
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'http://maven.snplow.com/releases' }
    }
}

// configure checkstyle
subprojects {
    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion '8.0'
    }
    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('analysis/checkstyle/checkstyle.xml')
        source 'src'
        include '*/java/**/*.java'
        ignoreFailures false
        showViolations true

        classpath = files()
    }
    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }
}

// base library config
configure(subprojects.findAll { it.path.startsWith(":library:") }) {
    apply plugin: 'com.android.library'

    beforeEvaluate {
        android {
            compileSdkVersion compileSdk

            defaultConfig {
                minSdkVersion minSdk
                targetSdkVersion targetSdk
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            lintOptions {
                lintConfig rootProject.file("analysis/lint/lint.xml")
            }
        }

        dependencies {
            // HACK: this works around a dependency issue with adobe analytics when running android tests
            androidTestImplementation "com.google.android.gms:play-services-wearable:${deps.playServices}"
        }
    }
}

// base feature config
configure(subprojects.findAll { it.path.startsWith(":feature:") }) {
    apply plugin: 'com.android.feature'
    apply plugin: 'org.ajoberstar.grgit'

    beforeEvaluate {
        android {
            compileSdkVersion compileSdk

            defaultConfig {
                minSdkVersion 19
                targetSdkVersion targetSdk

                versionName rootProject.version
                versionCode grgit.log(includes: ['HEAD']).size() + 4029047

                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            lintOptions {
                lintConfig rootProject.file("analysis/lint/lint.xml")
            }

            signingConfigs {
                release {
                    storeFile file(androidKeystorePath)
                    storePassword androidKeystoreStorePassword
                    keyAlias androidKeystoreKeyAlias
                    keyPassword androidKeystoreKeyPassword
                }
            }

            flavorDimensions "env"
            productFlavors {
                stage {
                    dimension "env"
                }
                production {
                    dimension "env"
                }
            }

            buildTypes {
                release {
                    minifyEnabled true
                    // XXX: currently not working for feature modules
//                    shrinkResources true
                    if (signingConfigs.release.storeFile.exists()) {
                        signingConfig signingConfigs.release
                    }
                }
            }

            variantFilter { variant ->
                // stage product flavors
                if (variant.flavors*.name.contains("stage")) {
                    // ignore for non-debug build types
                    if (!variant.buildType.name.equals("debug")) {
                        setIgnore(true)
                    }
                }
            }
        }
    }
}
