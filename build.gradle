buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://maven.google.com' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-alpha8'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        //noinspection GradleDependency
        classpath 'com.jakewharton:butterknife-gradle-plugin:8.5.1'
        classpath 'com.newrelic.agent.android:agent-gradle-plugin:5.14.0'
        classpath 'io.fabric.tools:gradle:1.23.0'
    }
}

plugins {
    id "org.ajoberstar.grgit" version "1.7.2" apply false
}

group 'org.cru.android'
version '5.0.0'

// define dependency versions for this project
ext {
    buildTools = "26.0.1"
    compileSdk = 26
    minSdk     = 15
    targetSdk  = 26
}
ext.deps = [
        advrecyclerview      : '0.10.6',
        androidLifecycle     : '1.0.0-alpha5',
        androidSupport       : '26.0.0',
        androidTestingSupport: '1.0.0',
        butterknife          : '8.7.0',
        calligraphy          : '2.3.0',
        crashlytics          : '2.6.8',
        eventbus             : '3.0.0',
        eventbusAnnotations  : '3.0.1',
        evernoteAndroidJob   : '1.1.11',
        facebookStetho       : '1.5.0',
        gtoSupport           : '1.1.2',
        guava                : '22.0-android',
        hamcrest             : '1.3',
        junit                : '4.12',
        leakcanary           : '1.5.1',
        lightweightStream    : '1.1.8',
        multidex             : '1.0.2',
        newrelic             : '5.14.0',
        playServices         : '10.2.6',
        retrofit2            : '2.3.0'
]

subprojects {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            name "maven.gcx.org"
            url "https://maven.gcx.org/"
            credentials {
                username gcxMavenRepositoryUsername
                password gcxMavenRepositoryPassword
            }
        }
        maven { url 'https://maven.google.com' }
        maven { url 'https://maven.fabric.io/public' }
    }
}

// configure checkstyle
subprojects {
    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion '8.0'
    }
    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('analysis/checkstyle/checkstyle.xml')
        source 'src'
        include '*/java/**/*.java'
        ignoreFailures false
        showViolations true

        classpath = files()
    }
    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }
}

// base library config
configure(subprojects.findAll { it.path.startsWith(":library:") }) {
    apply plugin: 'com.android.library'

    beforeEvaluate {
        android {
            buildToolsVersion buildTools
            compileSdkVersion compileSdk

            defaultConfig {
                minSdkVersion minSdk
                targetSdkVersion targetSdk
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            lintOptions {
                lintConfig rootProject.file("analysis/lint/lint.xml")
            }
        }
    }
}

// base feature config
configure(subprojects.findAll { it.path.startsWith(":feature:") }) {
    apply plugin: 'com.android.feature'
    apply plugin: 'org.ajoberstar.grgit'

    beforeEvaluate {
        android {
            buildToolsVersion buildTools
            compileSdkVersion compileSdk

            defaultConfig {
                minSdkVersion 19
                targetSdkVersion targetSdk

                versionName rootProject.version
                versionCode grgit.log(includes: ['HEAD']).size() + 4029047
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            lintOptions {
                lintConfig rootProject.file("analysis/lint/lint.xml")
            }

            signingConfigs {
                release {
                    storeFile file(androidKeystorePath)
                    storePassword androidKeystoreStorePassword
                    keyAlias androidKeystoreKeyAlias
                    keyPassword androidKeystoreKeyPassword
                }
            }

            buildTypes {
                release {
                    if (signingConfigs.release.storeFile.exists()) {
                        signingConfig signingConfigs.release
                    }
                }
            }
        }
    }
}
