import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

buildscript {
    ext {
        deps = [
                advrecyclerview    : '1.0.0',
                androidInstantApps : '1.1.0',
                appsFlyer          : '6.3.0',
                autoValue          : '1.8.1',
                circleindicator    : '2.1.6',
                dagger             : '2.35.1',
                eventbus           : '3.2.0',
                facebook           : '9.1.1',
                facebookFlipper    : '0.90.2',
                findbugs           : '3.0.2',
                googleTagManager   : '17.0.0',
                gradleAndroidPlugin: '4.2.1',
                grpc               : '1.38.0',
                gtoSupport         : '3.9.0-SNAPSHOT',
                guava              : '30.1.1-android',
                hamcrest           : '2.2',
                jacoco             : '0.8.7',
                jsoup              : '1.13.1',
                junit              : '4.13.2',
                kotlin             : '1.5.0',
                kotlinCoroutines   : '1.5.0',
                leakcanary         : '2.7',
                lottie             : '3.7.0',
                materialDesign     : '1.3.0',
                mockito            : '3.10.0',
                mockitoKotlin      : '2.2.0',
                moshi              : '1.12.0',
                okhttp3            : '4.9.1',
                okta               : '1.0.18',
                picasso            : '2.8',
                picassoTransforms  : '2.4.0',
                retrofit2          : '2.9.0',
                robolectric        : '4.5.1',
                scarlet            : '0.1.12',
                snowplow           : '2.0.1',
                splitties          : '3.0.0-beta01',
                taptargetview      : '1.13.2',
                timber             : '4.7.1',
                tinderStateMachine : '0.2.0',
                weakdelegate       : '1.0.1',
                youtubeplayer      : '10.0.5'
        ]
        deps.androidX = [
                activity          : '1.2.3',
                annotation        : '1.2.0',
                appCompat         : '1.3.0',
                archCore          : '2.1.0',
                browser           : '1.3.0',
                cardView          : '1.0.0',
                collection        : '1.1.0',
                concurrent        : '1.1.0',
                constraintLayout  : '2.0.4',
                core              : '1.5.0',
                databinding       : deps.gradleAndroidPlugin,
                fragment          : '1.3.4',
                hilt              : '1.0.0',
                lifecycle         : '2.3.0',
                loader            : '1.1.0',
                multidex          : '2.0.1',
                recyclerView      : '1.2.0',
                room              : '2.3.0',
                swipeRefreshLayout: '1.1.0',
                test              : '1.2.0',
                testJUnit         : '1.1.2',
                viewPager2        : '1.0.0',
                work              : '2.5.0',
        ]
        deps.firebase = [
                core          : '19.0.0',
                crashlytics   : '18.0.0',
                inAppMessaging: '20.0.0',
                messaging     : '22.0.0',
                perf          : '20.0.0',
        ]
        bundledContent = [
                tools               : ['kgp', 'fourlaws', 'satisfied', 'teachmetoshare'],
                attachments         : ['attr-banner', 'attr-banner-about'],
                languages           : ['en'],
                downloadTranslations: false,
        ]
        uris = [
                mobileContentApi: [
                        stage     : "https://mobile-content-api-stage.cru.org/",
                        production: "https://mobile-content-api.cru.org/"
                ]
        ]
    }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${deps.gradleAndroidPlugin}"
        classpath "com.google.dagger:hilt-android-gradle-plugin:${deps.dagger}"
        classpath 'com.google.firebase:perf-plugin:1.4.0'
        classpath 'com.google.firebase:firebase-appdistribution-gradle:2.1.2'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.6.1'
        classpath 'com.google.gms:google-services:4.3.8'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${deps.kotlin}"
    }
}

plugins {
    id "com.vanniktech.android.junit.jacoco" version "0.16.0"
    id "de.undercouch.download" version "4.1.1" apply false
    id "org.ajoberstar.grgit" version "4.1.0"
    id "org.jlleitschuh.gradle.ktlint" version "10.0.0"
}

version '5.5.1-SNAPSHOT'
ext.versionCode = grgit.log(includes:['HEAD']).size() + 4029765

// define dependency versions for this project
ext {
    applicationId = 'org.keynote.godtools.android'
    compileSdk    = 30
    minSdk        = 21
    targetSdk     = 30
}

// configure dependency resolution
allprojects {
    repositories {
        maven {
            url 'https://cruglobal.jfrog.io/cruglobal/list/maven-locals/'
            content {
                includeGroup 'org.ccci.gto.android'
                includeGroup 'org.ccci.gto.android.testing'
            }
        }
        maven {
            url 'https://jitpack.io'
            content { includeGroupByRegex 'com\\.github\\..*' }
        }
        google()
        mavenCentral()
        jcenter()
    }

    configurations.all { configuration ->
        configuration.resolutionStrategy {
            force "androidx.annotation:annotation:${deps.androidX.annotation}"
            force "androidx.appcompat:appcompat:${deps.androidX.appCompat}"
            force "androidx.fragment:fragment:${deps.androidX.fragment}"
            force "androidx.loader:loader:${deps.androidX.loader}"
            force "androidx.multidex:multidex:${deps.androidX.multidex}"
            force "com.google.code.findbugs:jsr305:${deps.findbugs}"
            force "junit:junit:${deps.junit}"

            // force the correct version of transitive coroutine libraries
            force "org.jetbrains.kotlinx:kotlinx-coroutines-reactive:${deps.kotlinCoroutines}"

            // XXX: Force a version of picasso that uses AndroidX.
            //      This can be removed once FIAM doesn't depend on Picasso 2.71828
            force "com.squareup.picasso:picasso:${deps.picasso}"

            // XXX: Force a newer version of grpc to work around an Android R+ crash with the FIAM library.
            //      Remove this override once FIAM depends on v1.30.0+ of grpc
            //      https://github.com/grpc/grpc-java/pull/6912
            //      https://jira.cru.org/projects/GT/issues/GT-1122
            force "io.grpc:grpc-okhttp:${deps.grpc}"
            force "io.grpc:grpc-protobuf-lite:${deps.grpc}"
            force "io.grpc:grpc-stub:${deps.grpc}"

            dependencySubstitution {
                substitute module('org.jetbrains.kotlin:kotlin-stdlib-jdk8') with module("org.jetbrains.kotlin:kotlin-stdlib-jdk7:${deps.kotlin}")

                // use the new condensed version of hamcrest
                substitute module('org.hamcrest:hamcrest-core') with module("org.hamcrest:hamcrest:${deps.hamcrest}")
                substitute module('org.hamcrest:hamcrest-library') with module("org.hamcrest:hamcrest:${deps.hamcrest}")

                // use regular moshi library instead of moshi-kotlin, any Kotlin moshi models should use moshi-kotlin-codegen instead
                substitute module('com.squareup.moshi:moshi-kotlin') with module("com.squareup.moshi:moshi:${deps.moshi}")
            }
        }
    }
}

// base app plugins
configure(subprojects.findAll { it.path == ":app" }) {
    apply plugin: 'com.android.application'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
}

// base library plugins
configure(subprojects.findAll { it.path.startsWith(":library:") || it.path.startsWith(":ui:") }) {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'

    dependencies {
        testImplementation "org.ccci.gto.android.testing:gto-support-okta:${deps.gtoSupport}"
    }
}

// base feature plugins
configure(subprojects.findAll { it.path.startsWith(":feature:") }) {
    apply plugin: "com.android.dynamic-feature"
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'

    android {
        flavorDimensions "env"
        productFlavors {
            stage {
                dimension "env"
            }
            production {
                dimension "env"
            }
        }
    }

    dependencies {
        implementation project(":app")
    }
}

// common config
subprojects {
    beforeEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion rootProject.compileSdk

                defaultConfig {
                    versionCode rootProject.versionCode

                    minSdkVersion rootProject.minSdk
                    targetSdkVersion rootProject.targetSdk
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_1_8.toString()
                    freeCompilerArgs += '-Xjvm-default=all'
                }
                lintOptions {
                    lintConfig rootProject.file("analysis/lint/lint.xml")
                }
                testOptions {
                    unitTests {
                        includeAndroidResources = true

                        all {
                            // increase unit tests max heap size
                            jvmArgs "-Xmx2g"

                            // enable only SDKs we actually support in robolectric
                            // TODO: SDK >= 29 requires Java 9 which we don't currently support
                            systemProperty 'robolectric.enabledSdks', (rootProject.minSdk..28).join(" ")
                        }
                    }
                }

                // only include stage variants for the debug buildType
                variantFilter { variant ->
                    if (variant.flavors*.name.contains("stage") && variant.buildType.name != "debug") setIgnore(true)
                }
            }

            dependencies {
                compileOnly "androidx.annotation:annotation:${deps.androidX.annotation}"
                compileOnly "com.google.code.findbugs:jsr305:${deps.findbugs}"

                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${deps.kotlin}"

                implementation "com.jakewharton.timber:timber:${deps.timber}"

                testImplementation "androidx.test.ext:junit:${deps.androidX.testJUnit}"
                testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:${deps.mockitoKotlin}"
                testImplementation "junit:junit:${deps.junit}"
                testImplementation "org.mockito:mockito-inline:${deps.mockito}"
                testImplementation "org.robolectric:robolectric:${deps.robolectric}"
            }
        }
    }
}

// configure checkstyle
subprojects {
    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion '8.0'
    }
    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('analysis/checkstyle/checkstyle.xml')
        source 'src'
        include '*/java/**/*.java'
        ignoreFailures false
        showViolations true

        classpath = files()
    }
    afterEvaluate {
        ['checkProductionRelease', 'checkProductionDebug', 'checkStageDebug', 'checkRelease', 'checkDebug', 'check'].each {
            project.tasks.findByName(it)?.dependsOn('checkstyle')
        }
    }
}

// configure ktlint
subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    ktlint {
        android = true
        reporters {
            reporter(ReporterType.PLAIN_GROUP_BY_FILE)
            reporter(ReporterType.CHECKSTYLE)
        }
    }
}

// jacoco config
junitJacoco {
    jacocoVersion = deps.jacoco
    includeNoLocationClasses = true
}
task jacocoTestReport {
    subprojects.each { dependsOn(it.tasks.withType(JacocoReport)) }
}
allprojects {
    if (gradle.startParameter.excludedTaskNames.contains("test")) {
        // exclude all test type tasks when the test task is excluded
        tasks.withType(Test).all { gradle.startParameter.excludedTaskNames += it.name }
    }
}
