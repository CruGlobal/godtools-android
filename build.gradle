import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        // HACK: load gradle-download-task before the kotlin plugin.
        //       This works around a classpath issue where the kotlin plugin includes an outdated version
        //       of public-suffix-list.txt
        //       see: https://github.com/michel-kraemer/gradle-download-task/issues/317#issuecomment-1635088067
        classpath libs.gradleDownloadTask

        classpath libs.android.gradlePlugin
        classpath libs.kotlin.gradlePlugin
    }
}

plugins {
    alias libs.plugins.hilt apply false
    alias libs.plugins.ksp apply false
    alias libs.plugins.ktlint
}

// configure ktlint
subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    ktlint {
        android = true
        version = libs.versions.ktlint.get()
        reporters {
            reporter(ReporterType.PLAIN_GROUP_BY_FILE)
            reporter(ReporterType.CHECKSTYLE)
        }
    }

    // HACK: workaround https://github.com/JLLeitschuh/ktlint-gradle/issues/524
    afterEvaluate {
        if (project.hasProperty('android')) {
            android.sourceSets.configureEach {
                java.srcDirs("src/$name/kotlin")
            }
        }
    }
}
