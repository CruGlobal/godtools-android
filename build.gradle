import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

buildscript {
    ext {
        deps = [
                advrecyclerview    : '1.0.0',
                androidInstantApps : '1.1.0',
                autoValue          : '1.8.2',
                circleindicator    : '2.1.6',
                dagger             : libs.versions.dagger.get(),
                eventbus           : libs.versions.eventbus.get(),
                findbugs           : '3.0.2',
                godtoolsToolParser : libs.versions.godtoolsMpp.get(),
                gradleAndroidPlugin: '7.0.3',
                grpc               : '1.41.0',
                gtoSupport         : libs.versions.gtoSupport.get(),
                hamcrest           : '2.2',
                jacoco             : '0.8.7',
                junit              : '4.13.2',
                kotlin             : '1.5.31',
                leakcanary         : '2.7',
                lottie             : '4.2.0',
                materialDesign     : libs.versions.materialComponents.get(),
                mockito            : '4.0.0',
                moshi              : '1.12.0',
                okhttp3            : libs.versions.okhttp3.get(),
                okta               : '1.2.1',
                picasso            : '2.8',
                picassoTransforms  : '2.4.0',
                retrofit2          : libs.versions.retrofit2.get(),
                robolectric        : '4.6.1',
                scarlet            : libs.versions.scarlet.get(),
                splitties          : libs.versions.splitties.get(),
                taptargetview      : '1.13.3',
                tinderStateMachine : '0.2.0',
                weakdelegate       : libs.versions.weakdelegate.get(),
                youtubeplayer      : libs.versions.youtubePlayer.get(),
        ]
        deps.androidX = [
                activity          : '1.4.0',
                annotation        : '1.2.0',
                appCompat         : libs.versions.androidx.appcompat.get(),
                archCore          : libs.versions.androidx.arch.get(),
                cardView          : '1.0.0',
                collection        : '1.1.0',
                concurrent        : '1.1.0',
                constraintLayout  : '2.1.1',
                core              : libs.versions.androidx.core.get(),
                databinding       : deps.gradleAndroidPlugin,
                fragment          : libs.versions.androidx.fragment.get(),
                hilt              : '1.0.0',
                lifecycle         : libs.versions.androidx.lifecycle.get(),
                recyclerView      : '1.2.1',
                room              : '2.3.0',
                swipeRefreshLayout: '1.1.0',
                test              : '1.2.0',
                testJUnit         : '1.1.3',
                viewPager2        : '1.0.0',
                work              : '2.7.0',
        ]
        deps.firebase = [
                inAppMessaging: '20.1.0',
        ]
        bundledContent = [
                tools               : ['kgp', 'fourlaws', 'satisfied', 'teachmetoshare'],
                attachments         : ['attr-banner', 'attr-banner-about'],
                languages           : ['en'],
                downloadTranslations: false,
        ]
        uris = [
                mobileContentApi: [
                        stage     : "https://mobile-content-api-stage.cru.org/",
                        production: "https://mobile-content-api.cru.org/"
                ]
        ]
    }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${deps.gradleAndroidPlugin}"
        classpath "com.google.dagger:hilt-android-gradle-plugin:${deps.dagger}"
        classpath 'com.google.firebase:perf-plugin:1.4.0'
        classpath 'com.google.firebase:firebase-appdistribution-gradle:2.2.0'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.8.0'
        classpath 'com.google.gms:google-services:4.3.10'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${deps.kotlin}"
    }
}

plugins {
    id "com.vanniktech.android.junit.jacoco" version "0.16.0"
    id "de.undercouch.download" version "4.1.2" apply false
    id "org.ajoberstar.grgit" version "4.1.0"
    id "org.jlleitschuh.gradle.ktlint" version "10.2.0"
}

version '5.6.2-SNAPSHOT'
ext.versionCode = grgit.log(includes:['HEAD']).size() + 4029765

// define dependency versions for this project
ext {
    applicationId = 'org.keynote.godtools.android'
    compileSdk    = 31
    minSdk        = 21
    targetSdk     = 31
}

// configure dependency resolution
allprojects {
    repositories {
        maven {
            url 'https://cruglobal.jfrog.io/artifactory/maven-mobile/'
            content {
                includeGroup 'org.ccci.gto.android'
                includeGroup 'org.ccci.gto.android.testing'
                includeGroup 'org.cru.godtools.kotlin'
                includeGroup 'org.cru.mobile.fork.antlr-kotlin'
            }
        }
        maven {
            url 'https://jitpack.io'
            content { includeGroupByRegex 'com\\.github\\..*' }
        }
        google()
        mavenCentral()
        jcenter {
            content {
                includeModule 'com.duolingo.open', 'rtl-viewpager'
                includeModule 'com.pierfrancescosoffritti.androidyoutubeplayer', 'core'
                includeModule 'com.sergivonavi', 'materialbanner'
            }
        }
    }

    configurations.all { configuration ->
        configuration.resolutionStrategy {
            force "androidx.lifecycle:lifecycle-viewmodel-ktx:${libs.versions.androidx.lifecycle.get()}"
            force "com.google.code.findbugs:jsr305:${deps.findbugs}"

            // XXX: Force a newer version of grpc to work around an Android R+ crash with the FIAM library.
            //      Remove this override once FIAM depends on v1.30.0+ of grpc
            //      https://github.com/grpc/grpc-java/pull/6912
            //      https://jira.cru.org/projects/GT/issues/GT-1122
            //      https://github.com/firebase/firebase-android-sdk/issues/2642
            force "io.grpc:grpc-okhttp:${deps.grpc}"
            force "io.grpc:grpc-protobuf-lite:${deps.grpc}"
            force "io.grpc:grpc-stub:${deps.grpc}"

            dependencySubstitution {
                // use the new condensed version of hamcrest
                substitute module('org.hamcrest:hamcrest-core') with module("org.hamcrest:hamcrest:${deps.hamcrest}")
                substitute module('org.hamcrest:hamcrest-library') with module("org.hamcrest:hamcrest:${deps.hamcrest}")
            }
        }
    }
}

// base app plugins
configure(subprojects.findAll { it.path == ":app" }) {
    apply plugin: 'com.android.application'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
}

// base library plugins
configure(subprojects.findAll { it.path.startsWith(":library:") || it.path.startsWith(":ui:") }) {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'

    dependencies {
        androidTestImplementation "org.ccci.gto.android.testing:gto-support-okta:${deps.gtoSupport}"
        testImplementation "org.ccci.gto.android.testing:gto-support-okta:${deps.gtoSupport}"
    }
}

// base feature plugins
configure(subprojects.findAll { it.path.startsWith(":feature:") }) {
    apply plugin: "com.android.dynamic-feature"
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'

    android {
        flavorDimensions "env"
        productFlavors {
            stage {
                dimension "env"
            }
            production {
                dimension "env"
            }
        }
    }

    dependencies {
        implementation project(":app")
    }
}

// common config
subprojects {
    beforeEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion rootProject.compileSdk

                defaultConfig {
                    versionCode rootProject.versionCode

                    minSdkVersion rootProject.minSdk
                    targetSdkVersion rootProject.targetSdk
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_11
                    targetCompatibility JavaVersion.VERSION_11
                }
                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_11.toString()
                    freeCompilerArgs += '-Xjvm-default=all'
                }
                lintOptions {
                    lintConfig rootProject.file("analysis/lint/lint.xml")
                }
                testOptions {
                    unitTests {
                        includeAndroidResources = true

                        all {
                            // increase unit tests max heap size
                            jvmArgs "-Xmx2g"
                        }
                    }
                }

                // only include stage variants for the debug buildType
                variantFilter { variant ->
                    if (variant.flavors*.name.contains("stage") && variant.buildType.name != "debug") setIgnore(true)
                }
            }

            dependencies {
                compileOnly "androidx.annotation:annotation:${deps.androidX.annotation}"
                compileOnly "com.google.code.findbugs:jsr305:${deps.findbugs}"

                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${deps.kotlin}"

                implementation libs.timber

                testImplementation "androidx.test.ext:junit:${deps.androidX.testJUnit}"
                testImplementation libs.mockito.kotlin
                testImplementation "junit:junit:${deps.junit}"
                testImplementation "org.mockito:mockito-inline:${deps.mockito}"
                testImplementation "org.robolectric:robolectric:${deps.robolectric}"
            }
        }
    }
}

// configure ktlint
subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    ktlint {
        android = true
        reporters {
            reporter(ReporterType.PLAIN_GROUP_BY_FILE)
            reporter(ReporterType.CHECKSTYLE)
        }
    }
}

// jacoco config
junitJacoco {
    jacocoVersion = deps.jacoco
    includeNoLocationClasses = true
}
allprojects {
    afterEvaluate {
        tasks.withType(Test) {
            jacoco.excludes = ['jdk.internal.*']
        }
    }
}
task jacocoTestReport {
    subprojects.each { dependsOn(it.tasks.withType(JacocoReport)) }
}
allprojects {
    if (gradle.startParameter.excludedTaskNames.contains("test")) {
        // exclude all test type tasks when the test task is excluded
        tasks.withType(Test).all { gradle.startParameter.excludedTaskNames += it.name }
    }
}
