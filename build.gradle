import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

buildscript {
    ext {
        bundledContent = [
                tools               : ['kgp', 'fourlaws', 'satisfied', 'teachmetoshare'],
                languages           : ['en'],
                downloadTranslations: false,
        ]
        uris = [
                mobileContentApi: [
                        stage     : "https://mobile-content-api-stage.cru.org/",
                        production: "https://mobile-content-api.cru.org/"
                ]
        ]
    }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath libs.firebase.appdistribution.gradlePlugin
        classpath libs.firebase.crashlytics.gradlePlugin
        classpath libs.firebase.perf.gradlePlugin
        classpath libs.google.services.gradlePlugin
    }
}

plugins {
    alias libs.plugins.grgit
    alias libs.plugins.hilt apply false
    alias libs.plugins.junitJacoco
    alias libs.plugins.ktlint
}

version '6.0.0-SNAPSHOT'
ext.versionCode = grgit.log(includes:['HEAD']).size() + 4029265

// define dependency versions for this project
ext {
    applicationId = 'org.keynote.godtools.android'
}

// configure dependency resolution
allprojects {
    repositories {
        maven {
            url 'https://cruglobal.jfrog.io/artifactory/maven-mobile/'
            content {
                includeGroup 'org.ccci.gto.android'
                includeGroup 'org.ccci.gto.android.testing'
                includeGroup 'org.cru.godtools.kotlin'
                includeGroup 'org.cru.mobile.fork.antlr-kotlin'
            }
        }
        maven {
            url 'https://jitpack.io'
            content {
                includeGroupByRegex 'com\\.github\\..*'
                excludeGroup 'com.github.ajalt.colormath'
            }
        }
        google()
        mavenCentral()
        jcenter {
            content {
                includeModule 'com.duolingo.open', 'rtl-viewpager'
                includeModule 'com.sergivonavi', 'materialbanner'
            }
        }
    }

    configurations.all { configuration ->
        configuration.resolutionStrategy {
            force libs.androidx.annotation
            force libs.androidx.lifecycle.viewmodel.ktx

            dependencySubstitution {
                // use the new condensed version of hamcrest
                substitute module('org.hamcrest:hamcrest-core') with module("${libs.hamcrest.get()}")
                substitute module('org.hamcrest:hamcrest-library') with module("${libs.hamcrest.get()}")
            }
        }
    }
}

// common config
subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            dependencies {
                compileOnly libs.androidx.annotation

                implementation libs.kotlin.stdlib

                implementation libs.timber

                testImplementation libs.androidx.test.junit
                testImplementation libs.junit
                testImplementation libs.mockito
                testImplementation libs.mockito.kotlin
                testImplementation libs.mockk
                testImplementation libs.robolectric

                // HACK: Fix Manifest merge errors for any classpath that contains the Okta module
                androidTestImplementation libs.gtoSupport.testing.okta
                testImplementation libs.gtoSupport.testing.okta
            }
        }
    }
}

// configure ktlint
subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    ktlint {
        android = true
        reporters {
            reporter(ReporterType.PLAIN_GROUP_BY_FILE)
            reporter(ReporterType.CHECKSTYLE)
        }
    }

    // HACK: workaround https://github.com/JLLeitschuh/ktlint-gradle/issues/524
    if (project.hasProperty('android')) {
        android.sourceSets.configureEach {
            java.srcDirs("src/$name/kotlin")
        }
    }
}

// jacoco config
junitJacoco {
    jacocoVersion = libs.versions.jacoco.get()
    includeNoLocationClasses = true
}
allprojects {
    afterEvaluate {
        tasks.withType(Test) {
            jacoco.excludes = ['jdk.internal.*']
        }
    }
}
task jacocoTestReport {
    subprojects.each { dependsOn(it.tasks.withType(JacocoReport)) }
}
allprojects {
    if (gradle.startParameter.excludedTaskNames.contains("test")) {
        // exclude all test type tasks when the test task is excluded
        tasks.withType(Test).all { gradle.startParameter.excludedTaskNames += it.name }
    }
}

if (project.hasProperty('gradleEnterprise')) {
    gradleEnterprise {
        buildScan {
            // automatically accept the scans.gradle.com TOS when running in GHA
            if (System.getenv("GITHUB_ACTIONS")) {
                termsOfServiceUrl = "https://gradle.com/terms-of-service"
                termsOfServiceAgree = "yes"
            }
        }
    }
}
