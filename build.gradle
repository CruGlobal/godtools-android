buildscript {
    ext.uris = [
            mobileContentApi: [
                    stage     : "https://mobile-content-api-stage.cru.org/",
                    production: "https://mobile-content-api.cru.org/"
            ]
    ]

    repositories {
        google()
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        //noinspection GradleDependency
        classpath 'com.jakewharton:butterknife-gradle-plugin:8.5.1'
        classpath 'com.newrelic.agent.android:agent-gradle-plugin:5.14.0'
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}

plugins {
    id "de.undercouch.download" version "3.4.3" apply false
    id "org.ajoberstar.grgit" version "2.2.1"
}

group 'org.cru.android'
version '5.0.15-SNAPSHOT'
ext.versionCode = (grgit.log(includes:['HEAD']).size() * 2) + 4026230

// define dependency versions for this project
ext {
    applicationId = 'org.keynote.godtools.android'
    compileSdk    = 27
    minSdk        = 15
    minSdkFeature = 21
    targetSdk     = 27
}
ext.deps = [
        adobeMobileServices  : '4.16.0',
        advrecyclerview      : '0.11.0',
        androidInstantApps   : '1.1.0',
        androidLifecycle     : '1.1.0',
        androidSupport       : '27.1.1',
        androidTestingSupport: '1.0.2',
        butterknife          : '8.7.0',
        calligraphy          : '2.3.0',
        crashlytics          : '2.9.4',
        eventbus             : '3.0.0',
        eventbusAnnotations  : '3.0.1',
        evernoteAndroidJob   : '1.2.5',
        facebookStetho       : '1.5.0',
        gtoSupport           : '1.2.2-SNAPSHOT',
        guava                : '25.0-android',
        hamcrest             : '1.3',
        junit                : '4.12',
        leakcanary           : '1.6.1',
        lightweightStream    : '1.1.9',
        mockito              : '2.17.0',
        multidex             : '1.0.3',
        newrelic             : '5.14.0',
        picassoTransforms    : '2.2.1',
        playServicesAnalytics: '16.0.1',
        powermock            : '2.0.0-beta.9',
        retrofit2            : '2.4.0',
        snowplow             : '0.7.0',
        thekey               : '2.0.0-SNAPSHOT',
        timber               : '4.7.0',
        roomVersion          : '1.0.0',
        archLifecycleVersion : '1.1.0'
]

subprojects {
    repositories {
        google()
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
        // XXX: temporary to use beta version of powermock
        maven { url 'https://dl.bintray.com/powermock/maven' }
        maven { url 'https://cruglobal.jfrog.io/cruglobal/list/maven-locals/' }
        mavenLocal()
    }
}

// configure checkstyle
subprojects {
    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion '8.0'
    }
    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('analysis/checkstyle/checkstyle.xml')
        source 'src'
        include '*/java/**/*.java'
        ignoreFailures false
        showViolations true

        classpath = files()
    }
    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }
}

// base library config
configure(subprojects.findAll { it.path.startsWith(":library:") || it.path.startsWith(":ui:") }) {
    apply plugin: 'com.android.library'

    beforeEvaluate {
        android {
            compileSdkVersion compileSdk

            defaultConfig {
                minSdkVersion minSdk
                targetSdkVersion targetSdk

                testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            lintOptions {
                lintConfig rootProject.file("analysis/lint/lint.xml")
            }
        }

        dependencies {
            compileOnly 'org.jetbrains:annotations:15.0'
        }
    }
}

// base feature config
configure(subprojects.findAll { it.path.startsWith(":feature:") }) {
    apply plugin: 'com.android.feature'

    beforeEvaluate {
        android {
            compileSdkVersion compileSdk

            defaultConfig {
                minSdkVersion minSdkFeature
                targetSdkVersion targetSdk

                versionName rootProject.version
                versionCode rootProject.versionCode

                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            lintOptions {
                lintConfig rootProject.file("analysis/lint/lint.xml")
            }

            signingConfigs {
                release {
                    storeFile file(androidKeystorePath)
                    storePassword androidKeystoreStorePassword
                    keyAlias androidKeystoreKeyAlias
                    keyPassword androidKeystoreKeyPassword
                }
            }

            flavorDimensions "env"
            productFlavors {
                stage {
                    dimension "env"
                }
                production {
                    dimension "env"
                }
            }

            buildTypes {
                release {
                    minifyEnabled true
                    // XXX: currently not working for feature modules
//                    shrinkResources true
                    if (signingConfigs.release.storeFile.exists()) {
                        signingConfig signingConfigs.release
                    }
                }
            }

            variantFilter { variant ->
                // stage product flavors
                if (variant.flavors*.name.contains("stage")) {
                    // ignore for non-debug build types
                    if (!variant.buildType.name.equals("debug")) {
                        setIgnore(true)
                    }
                }
            }
        }
    }
}
