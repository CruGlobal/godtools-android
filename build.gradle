buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'io.fabric.tools:gradle:1.21.2'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

apply from: 'versioning.gradle'

version '4.1.5-SNAPSHOT'

dependencies {
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-annotations:23.1.1'

    // keep this version of play services for now, newer versions may prompt users to upgrade before using the app
    //noinspection GradleDependency
    compile 'com.google.android.gms:play-services:4.3.23'

    compile 'org.ccci.gto.android:gto-support-core:0.7.2'
    compile 'org.ccci.gto.android:gto-support-db:0.7.2'

    compile 'com.crashlytics.sdk.android:crashlytics:2.5.5'
    compile 'com.google.guava:guava:18.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'

    // XXX: we need to remove usage of the Apache HttpClient
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId 'org.keynote.godtools.android'

        versionName project.version
        versionCode buildVersionCode()

        minSdkVersion 10
        targetSdkVersion 22

        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        buildConfigField "String", "GOOGLE_ANALYTICS_CLIENT_ID", "\"UA-325725-50\""
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    productFlavors {
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            buildConfigField "String", "BASE_URL_V2", "\"https://api.stage.godtoolsapp.com/godtools-api/rest/v2/\""
            buildConfigField "String", "BASE_URL", "\"https://api.stage.godtoolsapp.com/godtools-api/rest/\""
        }
        release {
            buildConfigField "String", "BASE_URL_V2", "\"https://api.godtoolsapp.com/godtools-api/rest/v2/\""
            buildConfigField "String", "BASE_URL", "\"https://api.godtoolsapp.com/godtools-api/rest/\""
        }
    }

    applicationVariants.all {
        buildConfigField "String", "EVERY_STUDENT_AUTHORITY", '"' + applicationId + '.everystudent.EveryStudentProvider"'
        resValue "string", "every_student_authority", applicationId + ".everystudent.EveryStudentProvider"
    }
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        name "maven.gcx.org"
        url "https://maven.gcx.org/"
        credentials {
            username gcxMavenRepositoryUsername
            password gcxMavenRepositoryPassword
        }
    }
    maven { url 'https://maven.fabric.io/public' }
}
