import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

buildscript {
    ext {
        deps = [
                adobeMobileServices: '4.18.2',
                advrecyclerview    : '1.0.0',
                androidInstantApps : '1.1.0',
                appsFlyer          : '5.4.3',
                assistedInject     : '0.6.0',
                circleindicator    : '2.1.4',
                dagger             : '2.29.1',
                eventbus           : '3.2.0',
                facebook           : '7.1.0',
                facebookFlipper    : '0.63.0',
                findbugs           : '3.0.2',
                gradleAndroidPlugin: '4.1.0',
                gtoSupport         : '3.7.0-SNAPSHOT',
                guava              : '30.0-android',
                hamcrest           : '2.2',
                hilt               : '2.29.1-alpha',
                jacoco             : '0.8.6',
                jsoup              : '1.13.1',
                junit              : '4.13.1',
                kotlin             : '1.4.10',
                kotlinCoroutines   : '1.4.0',
                leakcanary         : '2.5',
                lightweightStream  : '1.2.1',
                lottie             : '3.4.4',
                materialDesign     : '1.2.1',
                mockito            : '3.6.0',
                mockitoKotlin      : '2.1.0',
                moshi              : '1.11.0',
                okhttp3            : '3.12.12',
                picasso            : '2.8',
                picassoTransforms  : '2.3.1',
                retrofit2          : '2.6.4',
                robolectric        : '4.4',
                scarlet            : '0.1.10',
                snowplow           : '1.7.1',
                splitties          : '3.0.0-beta01',
                taptargetview      : '1.13.0',
                thekey             : '4.0.0',
                timber             : '4.7.1',
                tinderStateMachine : '0.2.0',
                weakdelegate       : '1.0.1',
                youtubeplayer      : '10.0.5'
        ]
        deps.androidX = [
                activity          : '1.1.0',
                annotation        : '1.1.0',
                appCompat         : '1.2.0',
                archCore          : '2.1.0',
                browser           : '1.2.0',
                cardView          : '1.0.0',
                collection        : '1.1.0',
                concurrent        : '1.1.0',
                constraintLayout  : '2.0.2',
                core              : '1.3.2',
                databinding       : deps.gradleAndroidPlugin,
                fragment          : '1.2.5',
                hilt              : '1.0.0-alpha02',
                lifecycle         : '2.2.0',
                loader            : '1.1.0',
                multidex          : '2.0.1',
                recyclerView      : '1.1.0',
                room              : '2.2.5',
                swipeRefreshLayout: '1.1.0',
                test              : '1.2.0',
                testJUnit         : '1.1.2',
                viewPager2        : '1.0.0',
                work              : '2.4.0',
        ]
        deps.firebase = [
                core          : '17.5.1',
                crashlytics   : '17.2.2',
                inAppMessaging: '19.1.1',
                messaging     : '21.0.0',
                perf          : '19.0.9',
        ]
        bundledContent = [
                tools               : ['kgp', 'fourlaws', 'satisfied', 'teachmetoshare'],
                attachments         : ['attr-banner', 'attr-banner-about'],
                languages           : ['en'],
                downloadTranslations: false,
        ]
        uris = [
                mobileContentApi: [
                        stage     : "https://mobile-content-api-stage.cru.org/",
                        production: "https://mobile-content-api.cru.org/"
                ]
        ]
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${deps.gradleAndroidPlugin}"
        classpath "com.google.dagger:hilt-android-gradle-plugin:${deps.hilt}"
        classpath 'com.google.firebase:perf-plugin:1.3.2'
        classpath 'com.google.firebase:firebase-appdistribution-gradle:2.0.1'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.3.0'
        classpath 'com.google.gms:google-services:4.3.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${deps.kotlin}"
    }
}

plugins {
    id "com.getkeepsafe.dexcount" version "2.0.0" apply false
    id "com.vanniktech.android.junit.jacoco" version "0.16.0"
    id "de.undercouch.download" version "4.1.1" apply false
    id "org.ajoberstar.grgit" version "4.1.0"
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
}

version '5.4.1-SNAPSHOT'
ext.versionCode = grgit.log(includes:['HEAD']).size() + 4029765

// define dependency versions for this project
ext {
    applicationId = 'org.keynote.godtools.android'
    compileSdk    = 30
    minSdk        = 19
    minSdkFeature = 21
    targetSdk     = 30
}

// configure dependency resolution
allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url 'https://cruglobal.jfrog.io/cruglobal/list/maven-locals/' }
    }

    configurations.all { configuration ->
        configuration.resolutionStrategy {
            force "androidx.annotation:annotation:${deps.androidX.annotation}"
            force "androidx.appcompat:appcompat:${deps.androidX.appCompat}"
            force "androidx.fragment:fragment:${deps.androidX.fragment}"
            force "androidx.loader:loader:${deps.androidX.loader}"
            force "androidx.multidex:multidex:${deps.androidX.multidex}"
            force "com.google.code.findbugs:jsr305:${deps.findbugs}"
            force "junit:junit:${deps.junit}"

            // force the correct version of transitive coroutine libraries
            force "org.jetbrains.kotlinx:kotlinx-coroutines-reactive:${deps.kotlinCoroutines}"

            // XXX: Force okhttp3 to older version to support older versions of Android.
            // XXX: This can go away once we bump minimum android version to API-21+
            force "com.squareup.okhttp3:okhttp:${deps.okhttp3}"

            dependencySubstitution {
                substitute module('org.jetbrains.kotlin:kotlin-stdlib-jdk8') with module("org.jetbrains.kotlin:kotlin-stdlib-jdk7:${deps.kotlin}")

                // use the new condensed version of hamcrest
                substitute module('org.hamcrest:hamcrest-core') with module("org.hamcrest:hamcrest:${deps.hamcrest}")
                substitute module('org.hamcrest:hamcrest-library') with module("org.hamcrest:hamcrest:${deps.hamcrest}")

                // use regular moshi library instead of moshi-kotlin, any Kotlin moshi models should use moshi-kotlin-codegen instead
                substitute module('com.squareup.moshi:moshi-kotlin') with module("com.squareup.moshi:moshi:${deps.moshi}")

                // HACK: utilize https://github.com/square/AssistedInject/pull/121 until it is in an official release
                substitute module('com.squareup.inject:assisted-inject-processor-dagger2') with module('com.github.frett.AssistedInject:assisted-inject-processor-dagger2:a298fbf')
            }
        }
    }
}

// base app plugins
configure(subprojects.findAll { it.path == ":app" }) {
    apply plugin: 'com.android.application'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
}

// base library plugins
configure(subprojects.findAll { it.path.startsWith(":library:") || it.path.startsWith(":ui:") }) {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
}

// base feature plugins
configure(subprojects.findAll { it.path.startsWith(":feature:") }) {
    apply plugin: "com.android.dynamic-feature"
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'

    android {
        flavorDimensions "env"
        productFlavors {
            stage {
                dimension "env"
            }
            production {
                dimension "env"
            }
        }
    }

    dependencies {
        implementation project(":app")
    }
}

// common config
subprojects {
    beforeEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion rootProject.compileSdk

                defaultConfig {
                    versionCode rootProject.versionCode

                    minSdkVersion rootProject.minSdk
                    targetSdkVersion rootProject.targetSdk
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_1_8.toString()
                    freeCompilerArgs += '-Xjvm-default=all'
                }
                lintOptions {
                    lintConfig rootProject.file("analysis/lint/lint.xml")
                }
                testOptions {
                    unitTests {
                        includeAndroidResources = true

                        all {
                            // increase unit tests max heap size
                            jvmArgs "-Xmx2g"

                            // enable only SDKs we actually support in robolectric
                            // TODO: SDK >= 29 requires Java 9 which we don't currently support
                            systemProperty 'robolectric.enabledSdks', (rootProject.minSdk..28).join(" ")
                        }
                    }
                }

                // only include stage variants for the debug buildType
                variantFilter { variant ->
                    if (variant.flavors*.name.contains("stage") && variant.buildType.name != "debug") setIgnore(true)
                }
            }

            dependencies {
                compileOnly "androidx.annotation:annotation:${deps.androidX.annotation}"
                compileOnly "com.google.code.findbugs:jsr305:${deps.findbugs}"

                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${deps.kotlin}"

                implementation "com.jakewharton.timber:timber:${deps.timber}"

                testImplementation "androidx.test.ext:junit:${deps.androidX.testJUnit}"
                testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:${deps.mockitoKotlin}"
                testImplementation "junit:junit:${deps.junit}"
                testImplementation "org.mockito:mockito-inline:${deps.mockito}"
                testImplementation "org.robolectric:robolectric:${deps.robolectric}"
            }
        }
    }
}

// configure checkstyle
subprojects {
    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion '8.0'
    }
    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('analysis/checkstyle/checkstyle.xml')
        source 'src'
        include '*/java/**/*.java'
        ignoreFailures false
        showViolations true

        classpath = files()
    }
    afterEvaluate {
        ['checkProductionRelease', 'checkProductionDebug', 'checkStageDebug', 'checkRelease', 'checkDebug', 'check'].each {
            project.tasks.findByName(it)?.dependsOn('checkstyle')
        }
    }
}

// configure ktlint
subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    ktlint {
        android = true
        reporters {
            reporter(ReporterType.PLAIN_GROUP_BY_FILE)
            reporter(ReporterType.CHECKSTYLE)
        }
    }
}

// jacoco config
junitJacoco {
    jacocoVersion = deps.jacoco
    includeNoLocationClasses = true
}
task jacocoTestReport {
    subprojects.each { dependsOn(it.tasks.withType(JacocoReport)) }
}
allprojects {
    if (gradle.startParameter.excludedTaskNames.contains("test")) {
        // exclude all test type tasks when the test task is excluded
        tasks.withType(Test).all { gradle.startParameter.excludedTaskNames += it.name }
    }
}
