buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'com.jakewharton:butterknife-gradle-plugin:8.5.1'
        classpath 'com.newrelic.agent.android:agent-gradle-plugin:5.12.2'
        classpath 'io.fabric.tools:gradle:1.22.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'newrelic'

apply from: 'versioning.gradle'

group 'org.cru.android'
version '4.3.3-SNAPSHOT'

// define dependency versions for this project
ext.deps = [
        androidSupport       : '25.3.0',
        androidTestingSupport: '0.5',
        butterknife          : '8.5.1',
        facebookStetho       : '1.4.2',
        gtoSupport           : '1.1.0-SNAPSHOT',
        guava                : '19.0',
        junit                : '4.12',
        playServices         : '9.0.2',
        retrofit2            : '2.2.0'
]

dependencies {
    compile 'com.android.support:appcompat-v7:' + deps.androidSupport
    compile 'com.android.support:support-annotations:' + deps.androidSupport
    compile 'com.android.support:design:' + deps.androidSupport

    compile 'com.google.android.gms:play-services-analytics:' + deps.playServices
    compile 'com.google.android.gms:play-services-gcm:' + deps.playServices

    compile 'org.ccci.gto.android:gto-support-api-okhttp3:' + deps.gtoSupport
    compile 'org.ccci.gto.android:gto-support-api-retrofit2:' + deps.gtoSupport
    compile 'org.ccci.gto.android:gto-support-db:' + deps.gtoSupport
    compile 'org.ccci.gto.android:gto-support-db-async:' + deps.gtoSupport
    compile 'org.ccci.gto.android:gto-support-gson:' + deps.gtoSupport
    compile 'org.ccci.gto.android:gto-support-jsonapi:' + deps.gtoSupport
    compile 'org.ccci.gto.android:gto-support-jsonapi-retrofit2:' + deps.gtoSupport

    compile 'com.crashlytics.sdk.android:crashlytics:2.6.7'
    compile 'com.newrelic.agent.android:android-agent:5.12.2'

    compile 'com.google.guava:guava:' + deps.guava
    compile 'com.jakewharton:butterknife:' + deps.butterknife
    compile 'com.squareup.retrofit2:retrofit:' + deps.retrofit2
    compile 'com.squareup.retrofit2:converter-gson:' + deps.retrofit2
    compile('com.squareup.retrofit2:converter-simplexml:' + deps.retrofit2)
            {
                exclude module: 'stax'
                exclude module: 'stax-api'
                exclude module: 'xpp3'
            };

    androidTestCompile 'com.android.support.test:runner:' + deps.androidTestingSupport
    androidTestCompile 'com.android.support.test:rules:' + deps.androidTestingSupport
    androidTestCompile 'com.android.support:support-annotations:' + deps.androidSupport
    testCompile 'junit:junit:' + deps.junit
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    debugCompile 'com.android.support:multidex:1.0.1'
    debugCompile 'org.ccci.gto.android:gto-support-leakcanary:' + deps.gtoSupport
    debugCompile 'com.facebook.stetho:stetho:' + deps.facebookStetho
    debugCompile 'com.facebook.stetho:stetho-okhttp3:' + deps.facebookStetho
    debugCompile 'com.facebook.stetho:stetho-urlconnection:' + deps.facebookStetho
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'

    compile project(path: ':godtools-renderer')

    annotationProcessor 'com.jakewharton:butterknife-compiler:' + deps.butterknife
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId 'org.keynote.godtools.android'
        versionName project.version
        versionCode buildVersionCode()

        minSdkVersion 10
        targetSdkVersion 25
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        proguardFiles 'proguard-rules.pro'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "MOBILE_CONTENT_API", "\"https://mobile-content-api-stage.cru.org/\""
        buildConfigField "String", "MOBILE_CONTENT_SYSTEM", "\"GodTools\""
        buildConfigField "String", "GOOGLE_ANALYTICS_CLIENT_ID", "\"UA-325725-50\""
        buildConfigField "String", "GROWTH_SPACES_URL", "\"https://www.growthspaces.org/api/v1/\""
        buildConfigField "String", "INTERPRETER_VERSION", "\"1\""
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    dexOptions {
        preDexLibraries !project.hasProperty('disablePreDex')
    }
    lintOptions {
        warning 'MissingTranslation'
    }
    packagingOptions {
        pickFirst 'META-INF/NOTICE.txt'
        pickFirst 'META-INF/LICENSE.txt'
    }
    signingConfigs {
        release {
            storeFile file(androidKeystorePath)
            storePassword androidKeystoreStorePassword
            keyAlias androidKeystoreKeyAlias
            keyPassword androidKeystoreKeyPassword
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            multiDexEnabled true

            buildConfigField "String", "BASE_URL_V2", "\"https://api.stage.godtoolsapp.com/godtools-api/rest/v2/\""
            buildConfigField "String", "BASE_URL", "\"https://api.stage.godtoolsapp.com/godtools-api/rest/\""
            buildConfigField "String", "NEW_RELIC_API_KEY", "\"AA2f0cb2d41f6ac7dcc256eaa9ca7e0f640859fd52\""
        }
        release {
            shrinkResources true
            minifyEnabled true
            if (signingConfigs.release.storeFile.exists()) {
                signingConfig signingConfigs.release
            }

            buildConfigField "String", "BASE_URL_V2", "\"https://api.godtoolsapp.com/godtools-api/rest/v2/\""
            buildConfigField "String", "BASE_URL", "\"https://api.godtoolsapp.com/godtools-api/rest/\""
            buildConfigField "String", "NEW_RELIC_API_KEY", "\"AAcaf7bbfe3ffda5dcb1455d3bc16b0c5da1cd71e9\""
        }
    }
    applicationVariants.all {
        buildConfigField "String", "EVERY_STUDENT_AUTHORITY", '"' + applicationId + '.everystudent.EveryStudentProvider"'
        resValue "string", "every_student_authority", applicationId + ".everystudent.EveryStudentProvider"
    }
}

repositories {

    mavenLocal()
    jcenter()

    maven { url 'https://maven.fabric.io/public' }

    maven {
        url "https://oss.sonatype.org/content/repositories/releases"
    }
}

apply from: "maven_publish.gradle"
afterEvaluate { project ->
    artifacts {
        archives artifactSourcesJar
        archives artifactJavadocJar
    }
}


allprojects {
    repositories {
        jcenter()
        maven {
            name "maven.gcx.org"
            url "https://maven.gcx.org/"
            credentials {
                username gcxMavenRepositoryUsername
                password gcxMavenRepositoryPassword
            }
        }
    }
}
