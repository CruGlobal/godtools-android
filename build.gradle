buildscript {
    repositories {
        mavenLocal()
        google()
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        //noinspection GradleDependency
        classpath 'com.jakewharton:butterknife-gradle-plugin:8.5.1'
        classpath 'com.newrelic.agent.android:agent-gradle-plugin:5.14.0'
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}

plugins {
    id "org.ajoberstar.grgit" version "2.1.1" apply false
}

group 'org.cru.android'
version '5.0.13-SNAPSHOT'

// define dependency versions for this project
ext {
    applicationId = 'org.keynote.godtools.android'
    compileSdk    = 27
    minSdk        = 15
    minSdkFeature = 21
    targetSdk     = 27
}
ext.deps = [
        advrecyclerview      : '0.10.6',
        androidInstantApps   : '1.1.0',
        androidLifecycle     : '1.1.0',
        androidSupport       : '27.1.1',
        androidTestingSupport: '1.0.2',
        butterknife          : '8.7.0',
        calligraphy          : '2.3.0',
        crashlytics          : '2.9.3',
        eventbus             : '3.0.0',
        eventbusAnnotations  : '3.0.1',
        evernoteAndroidJob   : '1.2.5',
        facebookStetho       : '1.5.0',
        gtoSupport           : '1.2.1',
        guava                : '25.0-android',
        hamcrest             : '1.3',
        junit                : '4.12',
        leakcanary           : '1.5.4',
        lightweightStream    : '1.1.9',
        mockito              : '2.17.0',
        multidex             : '1.0.3',
        newrelic             : '5.14.0',
        playServicesAnalytics: '16.0.0',
        powermock            : '2.0.0-beta.9',
        retrofit2            : '2.4.0',
        snowplow             : '0.6.2',
        thekey               : '2.0.0-SNAPSHOT',
        timber               : '4.7.0'
]

subprojects {
    repositories {
        mavenLocal()
        google()
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
        maven { url 'https://cruglobal.jfrog.io/cruglobal/list/maven-locals/' }
        maven {
            name "maven.gcx.org"
            url "https://maven.gcx.org/"
            credentials {
                username gcxMavenRepositoryUsername
                password gcxMavenRepositoryPassword
            }
        }
        // XXX: temporary to use beta version of powermock
        maven { url 'https://dl.bintray.com/powermock/maven' }
    }
}

// configure checkstyle
subprojects {
    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion '8.0'
    }
    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('analysis/checkstyle/checkstyle.xml')
        source 'src'
        include '*/java/**/*.java'
        ignoreFailures false
        showViolations true

        classpath = files()
    }
    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }
}

// base library config
configure(subprojects.findAll { it.path.startsWith(":library:") }) {
    apply plugin: 'com.android.library'

    beforeEvaluate {
        android {
            compileSdkVersion compileSdk

            defaultConfig {
                minSdkVersion minSdk
                targetSdkVersion targetSdk

                testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            lintOptions {
                lintConfig rootProject.file("analysis/lint/lint.xml")
            }
        }
    }
}

// base feature config
configure(subprojects.findAll { it.path.startsWith(":feature:") }) {
    apply plugin: 'com.android.feature'
    apply plugin: 'org.ajoberstar.grgit'

    beforeEvaluate {
        android {
            compileSdkVersion compileSdk

            defaultConfig {
                minSdkVersion minSdkFeature
                targetSdkVersion targetSdk

                versionName rootProject.version
                versionCode grgit.log(includes: ['HEAD']).size() + 4029047

                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            lintOptions {
                lintConfig rootProject.file("analysis/lint/lint.xml")
            }

            signingConfigs {
                release {
                    storeFile file(androidKeystorePath)
                    storePassword androidKeystoreStorePassword
                    keyAlias androidKeystoreKeyAlias
                    keyPassword androidKeystoreKeyPassword
                }
            }

            flavorDimensions "env"
            productFlavors {
                stage {
                    dimension "env"
                }
                production {
                    dimension "env"
                }
            }

            buildTypes {
                release {
                    minifyEnabled true
                    // XXX: currently not working for feature modules
//                    shrinkResources true
                    if (signingConfigs.release.storeFile.exists()) {
                        signingConfig signingConfigs.release
                    }
                }
            }

            variantFilter { variant ->
                // stage product flavors
                if (variant.flavors*.name.contains("stage")) {
                    // ignore for non-debug build types
                    if (!variant.buildType.name.equals("debug")) {
                        setIgnore(true)
                    }
                }
            }
        }
    }
}
